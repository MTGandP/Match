;;;;
;;;; match.lisp
;;;; ----------
;;;; Created by Michael Dickens on 2013-06-06.
;;;; 
;;;; Simple pattern matching for Common Lisp.
;;;; 

;; Do not print style-warning errors.
(declaim #+sbcl(sb-ext:muffle-conditions style-warning))


(defpackage :match
  (:use :common-lisp)
  (:export :matchp
	   :match
	   :unit-test
	   :unit-test-safe
	   :benchmark))

(in-package :match)

(defmacro define-match-error (name)
  "A basic macro for quickly defining new match errors."
  `(define-condition ,name (match-error)
     ((text :initarg :text :reader text))))

(define-condition match-error (error)
  ((text :initarg :text :reader text)))

(define-condition defpattern-error (match-error)
  ((text :initarg :text :reader text)))

(define-condition defpattern-args-mismatch (defpattern-error)
  ((text :initarg :text :reader text)))

(define-condition defpattern-wrong-number-special-args (defpattern-error)
  ((text :initarg :text :reader text)))

(define-condition defpattern-unknown-argument (defpattern-error)
  ((text :initarg :text :reader text)))

(define-condition malformed-match-args (match-error)
  ((text :initarg :text :reader text)))

(define-condition pattern-wrong-argnum (match-error)
  ((text :initarg :text :reader text)))

(define-condition undefined-pattern (match-error)
  ((text :initarg :text :reader text)))

(defun booleanp (x)
  (or (eq x t) (eq x nil)))

(defun do-nothing (x)
  (car (cons x nil)))

(defclass Pattern ()
  ((num-args :initarg :num-args)
   (matchp-test :initarg :matchp-test)
   (value-getter :initarg :value-getter)))

(defparameter *patterns* (make-hash-table)
  "The keys are symbols representing the names of patterns and the
  values are objects of type Pattern.")

(defun patternp (name)
  "Determines if the given symbol is a pattern name."
  (gethash name *patterns*))

;; TODO: Some parts of this have not been tested.
(defun basic-match-p (expr form)
  "Determines if the expression matches the form. Only uses certain
basic built-in patterns and NOT user-defined patterns."
  (cond
    ;; T matches everything.
    ((equal form t) t)

    ;; nil only matches nil.
    ((null form) (null expr))

    ;; A symbol matches everything.
    ((symbolp form) t)

    ;; A number must match the same number using =.
    ((and (numberp expr) (numberp form)) (= expr form))

    ;; Everything else is matched using equal.
    (t (equal expr form))))


(defun matchp (expr form)
  "Determines whether the expression matches the form."
  (cond
    ((and (consp form) (symbolp (car form)))
     (let ((pattern (gethash (car form) *patterns*)))
       (and 
	;; Match the outermost form
	(if pattern
	    (apply (slot-value pattern 'matchp-test) expr (cdr form))
	    (error 'undefined-pattern :text
		   (format nil "Undefined pattern ~a." (car form))))
	;; Recursively match inner forms but not if the form is QUOTE
	(if (equal (car form) 'quote)
	  t
	  (notany #'null
	    (loop for i from 0 below (length (cdr form)) collect
		 (handler-case
		     (matchp 
		      (funcall (slot-value pattern 'value-getter)
			       expr i)
		      (nth i (cdr form)))
		   (pattern-wrong-argnum (err) t))))))))
    (t (basic-match-p expr form))))

(defun arglist-count-args (args)
  "Counts the number of parameters in the given argument specification."
  (cond
    ((null args) 0)
    ((equal '&rest (car args)) 0)
    (t (1+ (arglist-count-args (cdr args))))))

(defun arglist-pos (val args)
  "Returns the position of val in args. If val is a &rest argument,
  returns a cons cell where the car is the argument position (not the
  list position) and the cdr is a &rest symbol."
  (let ((rest-pos (position '&rest args))
	(pos (position val args)))
    (if (and rest-pos (< rest-pos pos))
	(cons (- pos 1) '&rest)
	pos)))

;; TODO: if an argument's value is used, don't allow the pattern to
;; assign it to a variable, and vice versa.
;; TODO: Allow for a documentation string.
(defmacro defpattern (name args special-args pred &rest binds)
  "Defines a new pattern that can be used by matchp. Patterns do not
exist in the same namespace as functions.

name: The name of the pattern. A list may match the pattern if it
  begins with this symbol.
args: An argument specificer much like that for functions. When a
pattern is matched against, think of it as calling a function defined
by defpattern that takes in as arguments each value passed with the
pattern. The argument specifier may use &rest.
special-args: An argument specifier containing exactly one argument,
  which binds to the expression. By convention, this is given as
  (expr).
pred: The body for a predicate function that uses the values passed
in to args and special-args to determine whether the expression
matches the pattern.
binds: A list of forms where each form specifies how to bind a
variable. A single form contains two elements: first the name of the
argument, and second an expression that uses the  passed in to
  args and special-args to return the value of the given variable. 
  a &rest argument, it contains three forms: the first is the same as
  above; the second is a variable name which binds to the number of
  the argument passed in (that is, if there are 3 arguments wrapped
  into the &rest argument, then 0 is passed in to find the value for
  the first argument, 1 for the value of the second, and 2 for the
  value of the third); and the third is the same as
  the second argument given previously.
"
  (let ((argnum-sym (gensym))
	(expected-special-args 1))
  (if (not (= (length special-args) expected-special-args))
    (error 'defpattern-wrong-number-special-args
      (format nil
        "Wrong number of special arguments (~a found, ~a expected)"
	(length special-args) expected-special-args))
    `(setf (gethash ',name match:*patterns*)
     (make-instance 'Pattern 
      :num-args 
      ,(arglist-count-args args)
      
      :matchp-test
      (lambda (,@special-args ,@args) ,pred)

      :value-getter
      (lambda (,@special-args ,argnum-sym)
	(cond
	  ,@(nconc 
	    (mapcar 
	     (lambda (body)
	       (let ((pos (arglist-pos (car body) args)))
		 (cond
		   ((consp pos)
		    `((>= ,argnum-sym ,(car pos))
		      (let ((,(cadr body) (- ,argnum-sym ,(car pos))))
			,(caddr body))))
		   ((null pos)
		    (error 'defpattern-unknown-argument :text
			   (format nil 
				   "Pattern ~a has no argument ~a in argument list ~a"
				   name (car body) args)))
		   (t `((= ,argnum-sym ,pos) ,(cadr body))))))
	     binds)
	    `((t (error 'pattern-wrong-argnum :text
			(format nil "Pattern ~a has no argument number ~a"
				',name ,argnum-sym))))))))))))


;;; example defpattern for something more complicated and esoteric.
;;; Determines whether a non-empty list contains only even numbers.
(defpattern all-even (first &rest arglist)
  (expr)
    (and (listp expr)
	 (= (1+ (length arglist)) (length expr))
	 (>= (length expr) 1)
	 (reduce (lambda (pred x) (and pred (evenp x))) 
		 expr :initial-value t))
    (first (/ (car expr) 2))
    ;; TODO: try dividing each element by 2
    (arglist num (/ (nth num (cdr expr)) 2)))

;;
;; TODO: Make every nontrivial pattern user-defined instead of
;; built-in. Then use this to simplify the definitions of (bind-vars)
;; and (get-var-value). In the new (get-var-value), count the
;; arguments until reaching the (num)th argument and return its value.
;;


(defun bind-vars (expr form)
  "Treats symbols in form as variables. Creates a list of variables
in form where each element is a list containing first the variable,
then the expression in expr to which the variable is bound."
  (labels 
      ((rec (form-rec index path)
	 (cond
	   ;; Ignore the function name. All other symbols are
	   ;; considered variables.
	   ((and (= index 0) (consp form-rec))
	    (if (equal (car form-rec) 'quote)
		nil
		(rec (cdr form-rec) 1 path)))
	   ((consp form-rec)
	    (nconc (rec (car form-rec) 0 (cons (1- index) path))
		   (rec (cdr form-rec) (1+ index) path)))
	   ((and (symbolp form-rec) (not (booleanp form-rec)))
	    (list (list form-rec 
	      `(get-var-value ,expr ',form ',(reverse path)))))
	   (t nil))))
    (rec form 0 nil)))
	    

;; TODO: Do something about unused variables.
(defun get-var-value (expr form path)
  "Finds the (num)th variable in form and returns its corresponding
value in expr. If it does not understand the format of form, it
returns nil. Notice that this behavior is useful for some cases, but
it means that the function will quietly ignore invalid input."
  (cond
    ((and (consp expr) (consp form))
     (let ((pattern (gethash (car form) *patterns*))
	   (num (car path)))
       (when (not pattern)
	 (error 'undefined-pattern :text
		(format nil "Undefined pattern ~a" (car form))))
       (let ((next-val (funcall (slot-value pattern 'value-getter)
				expr num)))
	 (if (cdr path)
	     (get-var-value next-val (nth (1+ num) form) (cdr path))
	     next-val))))
    ;; If form is a symbol, it matches all of expr, so bind expr to
    ;; the symbol.
    ((symbolp form) expr)
    (t nil)))


(defmacro add-scope (expr form to-eval)
  "Puts the variables in form into scope and binds them to the
  corresponding values in expr. Then evaluates to-eval."
  (let ((var-list (bind-vars expr form)))
    (if var-list
	`(let ,var-list
	   ,to-eval)
	to-eval)))


(defun expand (val body)
  (when (consp body)
    (let ((form (first (car body))) (to-eval (second (car body))))
      (cons `((matchp ,val ',form)
	      (add-scope ,val ,form ,to-eval))
	    (expand val (cdr body))))))

(defun quote-forms (body)
  "Recursive function to put quotes in the right places in each form
  in body."
  (when (consp body)
    (cons (list `',(first (car body))
		(second (car body)))
	  (quote-forms (cdr body)))))

;; TODO: temporarily broken
(defmacro matchq (val &body body)
  "A more verbose but more flexible version of (match). This version
  will strictly evaluate predicates and bodies. For example: 
    (match val
      ((fun x y) ...)) ; no good--it will evaluate (fun x y)
    (match val
      ('(fun x y) ...)) ; this is what you want to do"
  `(cond ,@(expand val body)))


(defmacro match (val &body body)
  "Format: 
    (match val
      (pred1 body1)
      (pred2 body2)
      ...)
Runs through the list of predicates. As soon as it finds some predk
  such that (matchp val predk) returns t, it stops, evaluates bodyk,
  and returns. If predk contained any symbols other than function
  calls, it adds these variables to the local scope, binding them to
  the value in val that they matched. If you wish to use a generic
  placeholder symbol without binding it to a variable, use T."
  `(cond ,@(expand val body)))
;  `(cond ,@(expand val (quote-forms body))))

;; TODO: allow doc strings
(defmacro defmatch (name &body body)
  "A facility for defining functions that use pattern matching on the
arguments, similarly to Haskell or other functional languages."
  (let ((arg-sym (gensym)))
    `(defun ,name (&rest ,arg-sym)
       (match ,arg-sym
	      ,@(mapcar (lambda (x) 
			 `((list ,@(butlast x)) ,@(last x)))
		       body)))))


;;;;
;;;;
;;;; Default patterns
;;;;
;;;;

(defpattern list (&rest arglist) 
  (expr)
    (and (listp expr) 
	 (= (length arglist) (length expr)))
    (arglist num (nth num expr)))


;;; Matches a quoted s-expression. Notice that symbols must be treated
;;; with care within patterns: If you write 'x, it will be treated not
;;; as the symbol x, but as the pattern (quote x). To match the symbol
;;; 'x, use the pattern (quote 'x).
;;; Although quote is defined externally, it is special because a
;;; quote inside a pattern does not treat anything inside the quote as
;;; a pattern, so it terminates pattern nesting. This behavior is
;;; useful for matching quoted symbols or lists.
(defpattern quote (arg)
  (expr)
    (equal arg expr)
    (arg expr))

;;; defpattern for types
;; TODO: restructure program so that defpattern may bind args or it
;; may not. For example, (cons x xs) binds args x and xs, but 
;; (type 'number) does not bind any args.
(defpattern type (type-sym) 
  (expr)
    (typep expr type-sym))

;;; example defpattern for a cons cell
(defpattern cons (x xs) 
  (expr)
    (consp expr)
    (x (car expr))
    (xs (cdr expr)))
     

;;;;
;;;;
;;;; Tests.
;;;;
;;;;


(load "unit")

;;; Functions demonstrating pattern matching.
(defmatch factorial
  (0 1)
  (n (* n (factorial (1- n)))))

(defmatch tail-recursive-factorial 
  ;; If called with just 1 argument, add the other argument
  (n (tail-recursive-factorial n 1))
  ;; Do the actual computation
  (0 total total)
  (n total (tail-recursive-factorial (1- n) (* total n))))

(defmatch ackermann
  (0 n (1+ n))
  (m 0 (ackermann (1- m) 1))
  (m n (ackermann (1- m) (ackermann m (1- n)))))

(defmatch drop
  (n nil nil)
  (0 xs xs)
  (n (cons x xs) (drop (1- n) xs)))

(defmatch take
  (n nil nil)
  (0 xs nil)
  (n (cons x xs) (cons x (take (1- n) xs))))

(defun unit-test ()
  (unit:check 
    unit:verbose-off
    
    "Test proper compile-time error handling"
    (equal
     (handler-case 
	 (eval '(defpattern something (x) (wrong number special args) ()))
       (defpattern-wrong-number-special-args (err) "error found"))
     "error found")

    "Test basic expressions using primitives"
    (equal (match 3 (3 'yes))
	   'yes)
    (equal (match 3 (5 'yes) (t 'no))
	   'no)
    (equal (match "hello" ("HEllo" 'wrong) ("hello" 'right))
	   'right)

    "Test basic expressions using symbols and variables"
    (equal (let ((expr '('this 'has 4 'words))) (match expr (t expr)))
    	   '('this 'has 4 'words))
    (equal (let ((expr '(+ x 3))) 
	     (match expr (expr 'yes)))
	   'yes)

    ;; TODO: We should be able to use (type 'number).
    "Test user-defined type pattern"
    (equal (match 3 ((type number) 'yes) (t 'no))
	   'yes)
    (equal (match "hello" ((type number) 'yes) (t 'no))
	   'no)
    (equal (match 'world ((type number) 'num) ((type symbol) 'sym) (t 'no))
	   'sym)

    ;; "Test user-defined quote pattern"
    ;; ;; TODO: Get these to work. QUOTE is causing problems because it
    ;; ;; thinks QUOTE is a pattern, not an actual quote.
    (equal (let ((expr 'thing)) (match expr ('thing 'yes)))
	   'yes)
    (equal (match 'world ("world" 'wrong-str) 
		  ('other 'wrong-sym)
    		  ('world 'right))
    	   'right)
    (equal (match '(+ x 3) 
	     ('x 'wrong-structure) 
	     ('(+ y 3) 'wrong-symbol-name)
	     ('(+ x 4) 'wrong-number)
	     ('(+ x 3) 'right))
    	   'right)

    "Test user-defined cons pattern"
    (equal (match (list 1 2) ((cons x y) (list x y)))
	   '(1 (2)))
    (equal (match (list 1) ((cons x y) (list x y)))
	   (list 1 nil))

    "Test user-defined list pattern"
    (equal (match (list 1) ((list x) x))
	   1)
    (equal (match (list 1 2 3) ((list x y z) (+ x y z)))
	   6)
    (equal (match nil ((list) t) (t nil))
	   t)

    "Test user-defined all-even pattern"
    (equal (match (list 2 4 6) ((all-even x y z) (list x y z)))
	   (list 1 2 3))
    (equal (match (list 28) ((all-even x) (list x)))
	   (list 14))
    (equal (match (list 28) ((all-even x y) (list x y)))
	   nil)
    (equal (match (list 5) ((all-even x) (list x)))
	   nil)
    (equal
     (handler-case
	 (match (list 5 8) ((all-even) "accidentally succeeded"))
       (sb-int:simple-program-error (err) "error found"))
     "error found")

    "Test nested patterns"
    (equal (match (list 1 2) ((cons x (cons y z)) (list x y z)))
	   (list 1 2 nil))
    (equal (match (cons "not" "cons") 
		  ((cons x (cons y z)) (list x y z))
		  ((cons "cons" y) 'wrong)
		  ((cons "not" y) (concatenate 'string y " cell")))
	   "cons cell")
    (equal (match (list (list 1 2) 3) 
		  ((list (list x y) z) (list x y z)))
	   (list 1 2 3))
    (equal (match (list 4 5 6)
		  ((cons x (list y z)) (+ x y z)))
	   (+ 4 5 6))
    (equal (match (list 7 8)
		  ((cons x (cons 7 y)) 'wrong)
		  ((cons x (cons 8 y)) (list x y)))
	   (list 7 nil))

    "Test factorial function"
    (equal (factorial 0)
	   1)
    (equal (factorial 1)
	   1)
    (equal (factorial 2)
	   2)
    (equal (factorial 6)
	   720)

    "Test tail-recursive factorial function"
    (equal (tail-recursive-factorial 0)
	   1)
    (equal (tail-recursive-factorial 1)
	   1)
    (equal (tail-recursive-factorial 2)
	   2)
    (equal (tail-recursive-factorial 6)
	   720)

    "Test Ackermann function"
    (equal (ackermann 0 0) 
	   1)
    (equal (ackermann 1 0)
	   2)
    (equal (ackermann 2 0)
	   3)
    (equal (ackermann 4 0)
	   13)
    (equal (ackermann 0 1)
	   2)
    (equal (ackermann 0 2)
	   3)
    (equal (ackermann 1 1)
	   3)
    (equal (ackermann 2 2)
	   7)
    (equal (ackermann 3 3)
	   61)

    "Test drop function"
    (equal (drop 0 nil)
	   nil)
    (equal (drop 0 '(1 2 3))
	   '(1 2 3))
    (equal (drop 4 nil)
	   nil)
    (equal (drop 1 '(1 2 3))
	   '(2 3))
    (equal (drop 2 '(1 2 3))
	   '(3))
    (equal (drop 3 '(1 2 3))
	   nil)
    (equal (drop 4 '(1 2 3))
	   nil)

    "Test take function"
    (equal (take 0 nil)
	   nil)
    (equal (take 0 '(1 2 3))
	   nil)
    (equal (take 1 '(1 2 3))
	   '(1))
    (equal (take 2 '(1 2 3))
	   '(1 2))
    (equal (take 3 '(1 2 3))
	   '(1 2 3))
    (equal (take 4 '(1 2 3))
	   '(1 2 3))
    ))

(defun unit-test-safe ()
  "Calls (unit-test) and catches exceptions."
  (handler-case (unit-test) 
    (match-error (se)
      (format t "~a: ~a" se (slot-value se 'text)))))

(defun benchmark ()
  "Used to benchmark (match). Run this only after all the unit tests
  pass."
  (ackermann 3 7)
  (ackermann 3 9))

;; Resume printing style-warning errors.
(declaim #+sbcl(sb-ext:unmuffle-conditions style-warning))
